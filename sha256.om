// https://www.rfc-editor.org/rfc/rfc4634

struct sha256_ctx {
	a: int;
	b: int;
	c: int;
	d: int;
	e: int;
	f: int;
	g: int;
	h: int;
	len: int;
	block: _sha256_block;
}

struct _sha256_block {
	w0: int;
	w1: int;
	w2: int;
	w3: int;
	w4: int;
	w5: int;
	w6: int;
	w7: int;
	w8: int;
	w9: int;
	w10: int;
	w11: int;
	w12: int;
	w13: int;
	w14: int;
	w15: int;
}

struct _sha256_digest {
	d0: int;
	d1: int;
	d2: int;
	d3: int;
	d4: int;
}

sha256_init(r: *sha256_ctx) {
	r.a = 0x6a09e667;
	r.b = 0xbb67ae85;
	r.c = 0x3c6ef372;
	r.d = 0xa54ff53a;
	r.e = 0x510e527f;
	r.f = 0x9b05688c;
	r.g = 0x1f83d9ab;
	r.h = 0x5be0cd19;
	r.len = 0;
}

ror32(x: int, n: int): int {
	return ((x >> n) | (x << (32 - n))) & (-1 >> 32);
}

sha256_round(ctx: *sha256_ctx, w: *int, k: int) {
	var t1: int;
	var t2: int;
	var s0: int;
	var s1: int;
	var ch: int;
	var maj: int;
	var w16: int;

	s1 = ror32(ctx.e, 6); s1 = s1 ^ ror32(ctx.e, 11); s1 = s1 ^ ror32(ctx.e, 25);
	ch = (ctx.e & ctx.f) ^ ((~ctx.e) & ctx.g);
	t1 = (ctx.h + s1 + ch + k + w[0]) & (-1 >> 32);
	s0 = ror32(ctx.a, 2); s0 = s0 ^ ror32(ctx.a, 13); s0 = s0 ^ ror32(ctx.a, 22);
	maj = (ctx.a & ctx.b) ^ (ctx.a & ctx.c) ^ (ctx.b & ctx.c);
	t2 = (s0 + maj) & (-1 >> 32);

	s0 = ror32(w[16 - 15], 7); s0 = s0 ^ ror32(w[16 - 15], 18); s0 = s0 ^ (w[16 - 15] >> 3);
	s1 = ror32(w[16 - 2], 17); s1 = s1 ^ ror32(w[16 - 2], 19); s1 = s1 ^ (w[16 - 2] >> 10);
	w16 = (w[16 - 16] + s0 + w[16 - 7] + s1) & (-1 >> 32);

	w[0] = w[1];
	w[1] = w[2];
	w[2] = w[3];
	w[3] = w[4];
	w[4] = w[5];
	w[5] = w[6];
	w[6] = w[7];
	w[7] = w[8];
	w[8] = w[9];
	w[9] = w[10];
	w[10] = w[11];
	w[11] = w[12];
	w[12] = w[13];
	w[13] = w[14];
	w[14] = w[15];
	w[15] = w16;

	ctx.h = ctx.g;
	ctx.g = ctx.f;
	ctx.f = ctx.e;
	ctx.e = (ctx.d + t1) & (-1 >> 32);
	ctx.d = ctx.c;
	ctx.c = ctx.b;
	ctx.b = ctx.a;
	ctx.a = (t1 + t2) & (-1 >> 32);
}

sha256_rounds(ctx: *sha256_ctx, block: *byte) {
	var _w: _sha256_block;
	var w: *int;
	var r: sha256_ctx;
	w = &_w.w0;

	r.a = ctx.a;
	r.b = ctx.b;
	r.c = ctx.c;
	r.d = ctx.d;
	r.e = ctx.e;
	r.f = ctx.f;
	r.g = ctx.g;
	r.h = ctx.h;

	w[0] = (block[0] as int << 24)
		| (block[1] as int << 16)
		| (block[2] as int << 8)
		| block[3] as int;
	w[1] = (block[4] as int << 24)
		| (block[5] as int << 16)
		| (block[6] as int << 8)
		| block[7] as int;
	w[2] = (block[8] as int << 24)
		| (block[9] as int << 16)
		| (block[10] as int << 8)
		| block[11] as int;
	w[3] = (block[12] as int << 24)
		| (block[13] as int << 16)
		| (block[14] as int << 8)
		| block[15] as int;
	w[4] = (block[16] as int << 24)
		| (block[17] as int << 16)
		| (block[18] as int << 8)
		| block[19] as int;
	w[5] = (block[20] as int << 24)
		| (block[21] as int << 16)
		| (block[22] as int << 8)
		| block[23] as int;
	w[6] = (block[24] as int << 24)
		| (block[25] as int << 16)
		| (block[26] as int << 8)
		| block[27] as int;
	w[7] = (block[28] as int << 24)
		| (block[29] as int << 16)
		| (block[30] as int << 8)
		| block[31] as int;
	w[8] = (block[32] as int << 24)
		| (block[33] as int << 16)
		| (block[34] as int << 8)
		| block[35] as int;
	w[9] = (block[36] as int << 24)
		| (block[37] as int << 16)
		| (block[38] as int << 8)
		| block[39] as int;
	w[10] = (block[40] as int << 24)
		| (block[41] as int << 16)
		| (block[42] as int << 8)
		| block[43] as int;
	w[11] = (block[44] as int << 24)
		| (block[45] as int << 16)
		| (block[46] as int << 8)
		| block[47] as int;
	w[12] = (block[48] as int << 24)
		| (block[49] as int << 16)
		| (block[50] as int << 8)
		| block[51] as int;
	w[13] = (block[52] as int << 24)
		| (block[53] as int << 16)
		| (block[54] as int << 8)
		| block[55] as int;
	w[14] = (block[56] as int << 24)
		| (block[57] as int << 16)
		| (block[58] as int << 8)
		| block[59] as int;
	w[15] = (block[60] as int << 24)
		| (block[61] as int << 16)
		| (block[62] as int << 8)
		| block[63] as int;

	sha256_round(&r, w, 0x428a2f98);
	sha256_round(&r, w, 0x71374491);
	sha256_round(&r, w, 0xb5c0fbcf);
	sha256_round(&r, w, 0xe9b5dba5);
	sha256_round(&r, w, 0x3956c25b);
	sha256_round(&r, w, 0x59f111f1);
	sha256_round(&r, w, 0x923f82a4);
	sha256_round(&r, w, 0xab1c5ed5);
	sha256_round(&r, w, 0xd807aa98);
	sha256_round(&r, w, 0x12835b01);
	sha256_round(&r, w, 0x243185be);
	sha256_round(&r, w, 0x550c7dc3);
	sha256_round(&r, w, 0x72be5d74);
	sha256_round(&r, w, 0x80deb1fe);
	sha256_round(&r, w, 0x9bdc06a7);
	sha256_round(&r, w, 0xc19bf174);
	sha256_round(&r, w, 0xe49b69c1);
	sha256_round(&r, w, 0xefbe4786);
	sha256_round(&r, w, 0x0fc19dc6);
	sha256_round(&r, w, 0x240ca1cc);
	sha256_round(&r, w, 0x2de92c6f);
	sha256_round(&r, w, 0x4a7484aa);
	sha256_round(&r, w, 0x5cb0a9dc);
	sha256_round(&r, w, 0x76f988da);
	sha256_round(&r, w, 0x983e5152);
	sha256_round(&r, w, 0xa831c66d);
	sha256_round(&r, w, 0xb00327c8);
	sha256_round(&r, w, 0xbf597fc7);
	sha256_round(&r, w, 0xc6e00bf3);
	sha256_round(&r, w, 0xd5a79147);
	sha256_round(&r, w, 0x06ca6351);
	sha256_round(&r, w, 0x14292967);
	sha256_round(&r, w, 0x27b70a85);
	sha256_round(&r, w, 0x2e1b2138);
	sha256_round(&r, w, 0x4d2c6dfc);
	sha256_round(&r, w, 0x53380d13);
	sha256_round(&r, w, 0x650a7354);
	sha256_round(&r, w, 0x766a0abb);
	sha256_round(&r, w, 0x81c2c92e);
	sha256_round(&r, w, 0x92722c85);
	sha256_round(&r, w, 0xa2bfe8a1);
	sha256_round(&r, w, 0xa81a664b);
	sha256_round(&r, w, 0xc24b8b70);
	sha256_round(&r, w, 0xc76c51a3);
	sha256_round(&r, w, 0xd192e819);
	sha256_round(&r, w, 0xd6990624);
	sha256_round(&r, w, 0xf40e3585);
	sha256_round(&r, w, 0x106aa070);
	sha256_round(&r, w, 0x19a4c116);
	sha256_round(&r, w, 0x1e376c08);
	sha256_round(&r, w, 0x2748774c);
	sha256_round(&r, w, 0x34b0bcb5);
	sha256_round(&r, w, 0x391c0cb3);
	sha256_round(&r, w, 0x4ed8aa4a);
	sha256_round(&r, w, 0x5b9cca4f);
	sha256_round(&r, w, 0x682e6ff3);
	sha256_round(&r, w, 0x748f82ee);
	sha256_round(&r, w, 0x78a5636f);
	sha256_round(&r, w, 0x84c87814);
	sha256_round(&r, w, 0x8cc70208);
	sha256_round(&r, w, 0x90befffa);
	sha256_round(&r, w, 0xa4506ceb);
	sha256_round(&r, w, 0xbef9a3f7);
	sha256_round(&r, w, 0xc67178f2);

	ctx.a = (ctx.a + r.a) & (-1 >> 32);
	ctx.b = (ctx.b + r.b) & (-1 >> 32);
	ctx.c = (ctx.c + r.c) & (-1 >> 32);
	ctx.d = (ctx.d + r.d) & (-1 >> 32);
	ctx.e = (ctx.e + r.e) & (-1 >> 32);
	ctx.f = (ctx.f + r.f) & (-1 >> 32);
	ctx.g = (ctx.g + r.g) & (-1 >> 32);
	ctx.h = (ctx.h + r.h) & (-1 >> 32);
}

sha256_finish(ctx: *sha256_ctx, nblocks: int, data: *byte, len: int) {
	var _final: sha256_ctx;
	var final: *byte;
	var pad: int;
	var i: int;

	final = (&_final) as *byte;

	pad = nblocks * 512;

	loop {
		if len < 64 {
			break;
		}

		sha256_rounds(ctx, data);

		data = &data[64];
		len = len - 64;
		pad = pad + 512;
	}

	pad = pad + len * 8;

	i = 0;
	loop {
		if i == len {
			break;
		}

		final[i] = data[i];

		i = i + 1;
	}

	final[i] = 0x80 as byte;
	i = i + 1;

	loop {
		if i == 64 {
			break;
		}

		final[i] = 0 as byte;

		i = i + 1;
	}

	if len + 9 > 64 {
		sha256_rounds(ctx, final);

		i = 0;
		loop {
			if i == 64 {
				break;
			}

			final[i] = 0 as byte;

			i = i + 1;
		}
	}

	final[56] = (pad >> 56) as byte;
	final[57] = (pad >> 48) as byte;
	final[58] = (pad >> 40) as byte;
	final[59] = (pad >> 32) as byte;
	final[60] = (pad >> 24) as byte;
	final[61] = (pad >> 16) as byte;
	final[62] = (pad >> 8) as byte;
	final[63] = pad as byte;

	sha256_rounds(ctx, final);
}

sha256_digest(digest: *byte, ctx: *sha256_ctx) {
	digest[0] = (ctx.a >> 24) as byte;
	digest[1] = (ctx.a >> 16) as byte;
	digest[2] = (ctx.a >> 8) as byte;
	digest[3] = ctx.a as byte;
	digest[4] = (ctx.b >> 24) as byte;
	digest[5] = (ctx.b >> 16) as byte;
	digest[6] = (ctx.b >> 8) as byte;
	digest[7] = ctx.b as byte;
	digest[8] = (ctx.c >> 24) as byte;
	digest[9] = (ctx.c >> 16) as byte;
	digest[10] = (ctx.c >> 8) as byte;
	digest[11] = ctx.c as byte;
	digest[12] = (ctx.d >> 24) as byte;
	digest[13] = (ctx.d >> 16) as byte;
	digest[14] = (ctx.d >> 8) as byte;
	digest[15] = ctx.d as byte;
	digest[16] = (ctx.e >> 24) as byte;
	digest[17] = (ctx.e >> 16) as byte;
	digest[18] = (ctx.e >> 8) as byte;
	digest[19] = ctx.e as byte;
	digest[20] = (ctx.f >> 24) as byte;
	digest[21] = (ctx.f >> 16) as byte;
	digest[22] = (ctx.f >> 8) as byte;
	digest[23] = ctx.f as byte;
	digest[24] = (ctx.g >> 24) as byte;
	digest[25] = (ctx.g >> 16) as byte;
	digest[26] = (ctx.g >> 8) as byte;
	digest[27] = ctx.g as byte;
	digest[28] = (ctx.h >> 24) as byte;
	digest[29] = (ctx.h >> 16) as byte;
	digest[30] = (ctx.h >> 8) as byte;
	digest[31] = ctx.h as byte;
}

sha256(digest: *byte, data: *byte, dlen: int) {
	var ctx: sha256_ctx;
	sha256_init(&ctx);
	sha256_finish(&ctx, 0, data, dlen);
	sha256_digest(digest, &ctx);
}

sha256_hmac(mac: *byte, key: *byte, klen: int, data: *byte, dlen: int) {
	var _digest: _sha256_block;
	var digest: *byte;
	var _ipad: _sha256_block;
	var ipad: *byte;
	var _opad: _sha256_block;
	var opad: *byte;
	var ctx: sha256_ctx;
	var i: int;

	digest = (&_digest) as *byte;
	ipad = (&_ipad) as *byte;
	opad = (&_opad) as *byte;

	i = 0;
	loop {
		if i == 64 {
			break;
		}

		digest[i] = 0 as byte;

		i = i + 1;
	}

	if (klen <= 64) {
		i = 0;
		loop {
			if i == klen {
				break;
			}

			digest[i] = key[i];

			i = i + 1;
		}
	} else {
		sha256(digest, key, klen);
	}

	i = 0;
	loop {
		if i == 64 {
			break;
		}

		ipad[i] = digest[i] ^ (0x36 as byte);
		opad[i] = digest[i] ^ (0x5c as byte);

		i = i + 1;
	}

	sha256_init(&ctx);
	sha256_rounds(&ctx, ipad);
	sha256_finish(&ctx, 1, data, dlen);
	sha256_digest(digest, &ctx);

	sha256_init(&ctx);
	sha256_rounds(&ctx, opad);
	sha256_finish(&ctx, 1, digest, 32);
	sha256_digest(mac, &ctx);
}

sha256_update(ctx: *sha256_ctx, msg: *byte, len: int) {
	var o: int;
	var r: int;

	o = ctx.len & 63;
	if o != 0 {
		r = 64 - o;
		if len < r {
			memcpy(&(&ctx.block) as *byte[o], msg, len);
			ctx.len = ctx.len + len;
			return;
		} else {
			memcpy(&(&ctx.block) as *byte[o], msg, r);
			sha256_rounds(ctx, (&ctx.block) as *byte);
			ctx.len = ctx.len + r;
			len = len - r;
			msg = &msg[r];
		}
	}

	loop {
		if len < 64 {
			memcpy((&ctx.block) as *byte, msg, len);
			ctx.len = ctx.len + len;
			return;
		}

		sha256_rounds(ctx, msg);

		ctx.len = ctx.len + 64;
		len = len - 64;
		msg = &msg[64];
	}
}

sha256_final(digest: *byte, ctx: *sha256_ctx) {
	sha256_finish(ctx, ctx.len >> 6, (&ctx.block) as *byte, ctx.len & 63);
	sha256_digest(digest, ctx);
}
